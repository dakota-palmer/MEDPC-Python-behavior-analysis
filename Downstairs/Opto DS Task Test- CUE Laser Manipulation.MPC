\Opto DS and NS Manipulation Test
\Some DS & NS trials are randomly chosen as "Laser On" trials. On these trials, laser will turn on from cue onset until animal makes a PE or cue duration ends.

\In this code, the DS will trigger a siren. Rewards will be delivered in Port1 by Pump1. Pump duration increased to 2 sec.
\adapted from Opto Stage 5 DS Task inPortFixed.MPC and PulsePal Opto Laser DS Code_dp.MPC

\CONSTANTS
\^leftLever = 1
\^rightLever = 2
\^leftStimLight = 4
\^rightStimLight = 5
^DSTone = 6
^DSTone2 = 23
^NSwhiteNoise = 8
^Boxlight = 28
^Pump = 9
^Houselight = 7

^yellowLight = 25 \Laser delivery
^greenLight = 26 \DS
^redLight = 27 \NS

^Laser = 33

\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\Sound stuff from Jocelyn's old code\
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
\Sound frequencies and other parameters
DIM R=8 \soundParams
     ^ToneDur=10000         \R(0) Length of tone's on time, in ms
     ^ToneFreq=2600         \R(1) freq of tone
     ^SirenBaseFreq=6000    \R(2) starting (low) freq of the siren (Hz)
     ^SirenMaxFreq=8000     \R(3) ending (high) freq of the siren (Hz)
     ^SirenStepFreq=200     \R(4) Amount by which to increase or decrease the frequency every 10 ms
     ^RFTime=1              \R(5) rise time in ms
     ^Amplitude=85          \R(6) amplitude (dB) of tones (note this is not actual amplitude;
                            \95 dB corresponds to ~85 dB @ 4 kHz on the box floor)
                            \R(7) is the current freq of siren
     ^SirenAmp=93           \R(8) is siren amplitude




\INPUTS
^PortEntry= 5 \this is the transient port entry sensor
^PortEntrySus= 6 \this is the sustained port entry sensor
^Lick = 7
\^leftLeverPress = 1
\^rightLeverPress = 3

\ Inter-trial intervals in seconds
LIST X = 10, 20, 30, 40, 50, 10, 20, 30, 40, 50   \ITI


\ Choose DS or NS for each trial
LIST Y = 0,0,0,0,0,1,1,1,1,1 \listCueType

\ Choose Laser ON or OFF for each DS trial   \OFF= 0  and ON = 1
LIST C = 0,0,0,0,0,1,1,1,1,1  \listProbDSLaser

\ Choose Laser ON or OFF for each NS trial   \OFF= 0  and ON = 1
LIST T = 0,0,0,0,0,1,1,1,1,1 \listProbNSLaser


Var_Alias Startup Delay (Sec)                 = A(0)
Var_Alias Number of trials                    = A(1)
Var_Alias DS duration                         = A(2)
Var_Alias Pump Delay (Sec)                    = A(3)
Var_Alias Pump duration (Sec)                 = A(4)
Var_Alias Paired (0), or Unpaired (1) DS-US   = A(5)




DIM A=9 \stageParams
    \A(0)=Delay before 1st ITI in seconds (default 120 sec)
    \A(1)=Number of DS trials (default 60)
    \A(2)=Duration of DS phase in seconds (default 10 sec)
    \A(3)=Delay between CS+ ON and US ON for paired
    \A(4)=Pump duration in sec (default 5 sec)
    \A(5)=0 if paired DS-US, 1 if unpaired




\WORKING VARIABLES
DIM B=81 \workingVars
    \B(0) = 1" in MED time units, used for time input calculations
    \B(1) = Current trial #
    \B(2) = Current ITI length in seconds
    \B(3) = Current phase of trial, where 1=preDS 2=DS 3=postDSPlus 0=ITI 4=NS 5=postNS
    \B(4) = Box number of control box
    \B(5) = Reward count

    \B(6) = Total PEs
    \B(7) = PEs in ITI
    \B(8) = PEs in preDS
    \B(9) = PEs in DS
    \B(10)= PEs in postDS
    \B(11)= PEs in NS
    \B(12)= PEs in postNS

    \B(13)= Event timer in .01" units
    \B(14)= Current trial DS (1) versus NS (0)
    \B(15)= Program version number
    \B(16)= Elapsed seconds in current context
    \B(17)= Number of DS
    \B(18)= Number of NS
    \B(19)= Onset time of most recent cue
    \B(20)= Onset time of most recent cue-induced port entry & licking

    \B(21)= Number of DS trials with a port entry
    \B(22)= Number of NS trials with a port entry
    \B(23)= DS with PE to Number of DS Ratio
    \B(24)= NS with PE to Number of NS Ratio
    \B(25)= DS PE Mean Latency
    \B(26)= NS PE Mean Latency
    \B(27)= Not in use in this program
    \B(28)= Not in use in this program
    \B(29)= Total Time in Port
    \B(30)= Port Time in ITI
    \B(31)= Port Time in Pre DS/NS
    \B(32)= Port Time in DS
    \B(33)= Port Time in Post DS
    \B(34)= Port Time in NS
    \B(35)= Port Time in Post NS

    \B(36)= Total Licks
    \B(37)= Licks in ITI \These arrays are never explicitly referenced in the script, but what it does is use B(37)+B(3) as an index. Since B(3) 1=preDS 2=DS 3=postDSPlus 0=ITI 4=NS 5=postNS, B(37)=ITI B(38)=preDS B(39)=DS B(40)=postDS B(41)=NS B(42)= postNS... but only IF B(3) is correct
    \B(38)= Licks in Pre DS/NS \? so this collapses all pre-cue licks? 1=preDS +37
    \B(39)= Licks in DS \ 2=DS +37
    \B(40)= Licks in Post DS \3=postDS +37
    \B(41)= Licks in NS \ 4=NS +37
    \B(42)= Licks in Post NS 5=postNS +37
    \B(43)= Number of DS trials with licks
    \B(44)= Number of NS trials with licks
    \B(45)= DS with Licks to Number of DS Ratio
    \B(46)= NS with Licks to Number of NS Ratio
    \B(47)= Licks in 5 sec post DS port entry
    \B(48)= Licks in 5 sec post NS port entry

    \B(49)= Number of trial state (B(3)) transitions \dp added 5.30.21 for indexing of new array W

    \B(50) = Current trial Laser OFF (0) or Laser ON (1)
    \B(51)= Total Laser trials

\DATA ARRAYS
DIM H=61   \DStime \ DS onset time per trial
DIM I=61   \NStime \ NS onset time per trial
DIM J=61   \UStime \ US onset time per trial

DIM K=9999 \PEtime \ PE timestamps in sec
DIM L=9999 \PEdur \ PE durations in sec
DIM M=31   \DSPElat \ Latency to PE following DS per trial
DIM N=31   \NSPElat \ Latency to PE following NS per trial
DIM O=31   \DSPE1 \ Time of first PE following DS
DIM P=31   \NSPE1 \ Time of first PE following NS
DIM S=9999 \lickTime \ LickTimestamps in sec
DIM V=31   \lickDSpost \ 5-sec Licks after first PE following DS
DIM U=31   \lickNSpost \ 5-sec Licks after first PE following NS

DIM W=9999 \trialState \record of trial states B(3) dp added 5/30/21

DIM D= 9999 \laserTime \Laser timestamps
DIM E= 61 \laserDStrial \DS Laser trial array
DIM F= 61 \laserNStrial \NS Laser trial array

\Z signals (within box):
    \Z1 = Start initial delay
    \Z2 = Start DS
    \Z3 = Start US (possibly with delay)
    \Z4 = Start new trial (pre-DS)
    \Z5 = Start delay before session
    \Z6 = Start NS
    \Z9 = End of last trial
    \Z10 = Test Complete
    \Z11 = Signal that 5" has passed since first PE after DS
    \Z12 = Siren on
    \Z13 = Siren off

    \Z14= Laser was ON
    \Z15= Laser was OFF


\K signals (across all boxes):
\K1-K16= Used to report loaded boxes and choose lowest-numbered one as control
\K21 = Start 1st half of ITI
\K22 = Start 2nd half of ITI
\K23 = Start pre-DS period
\K24 = Start DS period
\K25 = Start post-DS period
\K26 = Start NS period
\K27 = Start post-NS period

\K28= Laser ON


S.S.1, \ Session clock in .01" units
S1,
  #K21:--->S2 \ K21= Start first half of ITI

S2,
  .01": ADD B(13)--->SX \ B(13)= Event timer in .01" units; milliseconds



S.S.2, \ Test the boxes
S1,
   .1": SHOW 1, Test, A(1);
       SET R(0)= ^ToneDur;
       SET R(1)= ^ToneFreq;
       SET R(2)= ^SirenBaseFreq;
       SET R(3)= ^SirenMaxFreq;
       SET R(4)= ^SirenStepFreq;
       SET R(5)= ^RFTime;
       SET R(6)= ^Amplitude;
       SET R(7)= 0;
       SET R(8)= ^SirenAmp;
       ~SetRack(MG,3);~; \Set Rack to 3
       ~InitANL926RP(MG,3,790);~; \ Reset ANL-926
       ~SetRF(MG, BOX, R[5]);~;
       ON ^HouseLight--->S2
S2,
  #R^PortEntry: ON ^NSwhiteNoise, ^Pump ;~setFreq(MG,BOX,R[1]);~; ~setDur(MG,BOX,R[0]);~; ~onAmp(MG, BOX, R[6]);~--->S3
S3,
   2":~toneOff(MG, BOX);~;OFF ^NSwhiteNoise, ^Pump; Z12; ON ^greenLight, ^yellowLight, ^redLight ---> S4
S4,
   2": Z13; OFF ^HouseLight, ^greenLight, ^yellowLight, ^redLight--->S5
S5,
  .1": SHOW 1, Ready,A(1); Z10--->S6
S6,
   5':--->SX



S.S.3, \ Initialization and trial control for all boxes. DS duration is A(2) seconds.
S1, \Initialize variables
  .01": SET A(5)=0;   \This value specifies the protocol: 0 for paired DS-US or 1 for unpaired
        SET A(0)=10, A(1)=60, A(2)=10, A(3)=10, A(4)=2;
        SET B(0)=1";
        SET H(1)=-987.987, I(1)=-987.987, J(1)=-987.987, K(1)=-987.987, L(1)=-987.987, W(1)= -987.987--->SX
  #Z10:--->S2

S2, \Let all boxes know this box is loaded, to select unique control box in S.S.4
  #START: KBOX--->S3   \ie, send K1 if this is box 1, K2 if box 2, etc.
  #Z5:--->S3

S3, \Begin initial ITI
  #K21: ON ^Houselight--->S6

S4, \Begin 1st half of regular ITI
  #K21: SET B(3)=0; SET W(B(49))=B(3);  SET W(B(49)+1)=-987.987; ADD B(49)--->S5

S5, \Begin 2nd half of regular ITI, and US if unpaired
  #K22: IF A(5)=1 [@T2,@F2] \A(5)=0 from S.S.2 \true only if unpaired protocol (A(5))=1
          @T2: Z3--->S6
          @F2:--->S6

S6, \Begin Pre-DS, unless all trials have run and session is over
  #K23: SET B(3)=1; SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49);
        IF B(1) < A(1) [@T3,@F3]
          @T3: ADD B(1); SET I(B(1))=0, I(B(1)+1)=-987.987;
               SET J(B(1))=0, J(B(1)+1)=-987.987; Z4--->S7
          @F3: OFF ^Houselight; Z9--->S9

          \Is the current trial # is < the set number of trials?
          \If false, then end the session ---> S9

S7, \Begin DS, and also US (after delay) if paired
  #K24: SET B(3)=2; SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49); Z2; \Z2 starts the DS trial epoch within-box
        IF A(5)=0 [@T4,@F4]
          @T4: Z3--->S8 \if unpaired DS-US protocol (A(5)=0), trigger Z3 signal to initiate US
          @F4:--->S8
  #K26: SET B(3)=4; SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49); Z6 ---> S8 \Z6 starts the NS trial epoch within box

S8, \Begin Post-DS
  #K25: ---> S4 \go back to state 4 and wait for a new DS or NS trial start signal
  #K27: ---> S4

S9, \A couple of extra seconds to update display, then quit
  2":--->STOPABORTFLUSH



S.S.4, \ Trial and ITI timing, mainly for the control box
S1, \Let the lowest-numbered running box (starting with 1) assume control
  #K1:  Z5; SET B(4)= 1--->S2
  #K2:  Z5; SET B(4)= 2--->S2
  #K3:  Z5; SET B(4)= 3--->S2
  #K4:  Z5; SET B(4)= 4--->S2
  #K5:  Z5; SET B(4)= 5--->S2
  #K6:  Z5; SET B(4)= 6--->S2
  #K7:  Z5; SET B(4)= 7--->S2
  #K8:  Z5; SET B(4)= 8--->S2
  #K9:  Z5; SET B(4)= 9--->S2
  #K10: Z5; SET B(4)=10--->S2
  #K11: Z5; SET B(4)=11--->S2
  #K12: Z5; SET B(4)=12--->S2

S2, \Pre-session delay, then check if this is the control box
  A(0)*B(0)#T: IF B(4)=BOX [@T1,@F1]  \If control, choose 1st ITI and proceed
                 @T1: K21; RANDD B(2)=X--->S3
                 @F1:--->S15        \Non-control: Go into passive state

S3, \Signal end of 1st half of ITI to all boxes, in case this is unpaired protocol
  (B(2)/2-A(3))*B(0)#T: K22--->S4   \Take into account pump delay for mid-ITI delivery

S4, \Signal end of 2nd half of ITI to all boxes
  (B(2)/2+A(3))*B(0)#T: K23--->S5

S5, \Pick NS or DS
   .1": RANDD B(14)=Y--->S6 \select from list Y DS (1) or NS (0)

S6, \Trigger NS or DS
   .1": IF B(14)=0 [@T3,@F3]
          @T3: --->S11    \NS trigger
          @F3: --->S7    \DS Trigger

S7, \Choose Laser or No Laser for DS
    .1": RANDD B(50)=C--->S8


S8, \Signal end of Pre-DS interval to all boxes. Send Laser ON signal
  10": IF B(50)=0 [@T1, @F1]
    @T1:K24--->S9 \after pre-cue delay, send K24 pulse to initate DS trial in all boxes
    @F1:K24; K28--->S9 \If Laser on trial, also send K28 to signal laser ON

S9, \Signal end of DS interval to all boxes after full cue duration
  A(2)*B(0)#T: K25--->S10

S10, \Signal end of post-DS interval to all boxes, choose next ITI
  10": K21; RANDD B(2)=X--->S3

S11, \Choose Laser or No Laser for NS
    .1": RANDD B(50)=T--->S12

S12, \Signal end of Pre-NS interval to all boxes
  10": IF B(50)=0 [@T1, @F1]
    @T1:  K26--->S13
    @F1: K26; K28 --->S13 \Send K28 Signal for Laser ON

S13, \Signal end of NS interval to all boxes after full cue duration
   A(2)*B(0)#T: K27--->S14

S14, \Signal end of post-NS interval to all boxes, choose next ITI
   10" : K21; RANDD B(2)=X--->S3

S15, \Passive state for non-control boxes
  60':--->SX

S.S.5, \ DS Laser Array
S1,
    #K24:--->S2
S2,
    #Z14: SET E(B(17))=1; SET E(B(17)+1)=-987.987--->S1
    1": SET E(B(17))=0; SET E(B(17)+1)=-987.987--->S1


S.S.6, \ NS Laser Array
S1,
    #K26:--->S2
S2,
    #Z14: SET F(B(18))=1; SET F(B(18)+1)=-987.987--->S1
    1": SET F(B(18))=0; SET F(B(18)+1)=-987.987--->S1

S.S.25, \ Laser stimulation start and stop

S1,
    .1": ON ^Laser--->S2 \Laser TTL for Pulse Pal is inverted (TTL offset triggers Laser), so ON^Laser turns the lasers off by default until a K28 signal is sent for OFF^Laser to turn the lasers on
S2,
    #K28: OFF ^Laser; Z14; ON ^yellowLight; ADD B(51); SET D(B(51))=B(13)/100, D(B(51)+1)=-987.987--->S3 \turn on laser and yellow light
S3,
    #R^PortEntry: ON ^Laser; OFF^yellowLight --->S2 \if there is a port entry detected, cease laser, turn off yellow light
    #K25: ON ^Laser; OFF^yellowLight --->S2 \Or if the cue duration ends do the same; dp changed this to K25, which signals DS trial end. Previously was K23 which would have been ITI end
    #K27: ON ^Laser; OFF^yellowLight --->S2 \also needs to accomodate NS trial end with K27 signal




S.S.7, \ Siren control

S1,
  #Z12: set R(7) = R(2); ~setFreq(MG,BOX,R[2]);~; ~onAmp(MG, BOX, R[8]);~; SET H(B(17))=B(13)/100--->S2  \Currently, Tone one is substituted.
S2,
 .01":set R(7) = R(7) + ^SirenStepFreq; ~setFreq(MG,BOX,R[7]);~; ~onAmp(MG, BOX, R[8]);~; if R(7)>=^SirenMaxFreq [@GoDown, @Continue]
    @GoDown: --->s3
    @Continue: ---> s2
 #Z13: ~toneOff(MG, BOX);~ ---> S1
s3,
 .01":set R(7) = R(7) - ^SirenStepFreq; ~setFreq(MG,BOX,R[7]);~; ~onAmp(MG, BOX, R[8]);~; if R(7)<=^SirenBaseFreq [@GoUp, @Contin]
    @GoUp: --->s2
    @Contin: ---> s3
 #Z13: ~toneOff(MG, BOX);~; ---> S1



S.S.8, \ DS control \dp added cue light for duration of cue presenation here
S1,
  #Z2: Z12; SET H(B(17))=B(13)/100; ON ^greenLight--->S2 \once the DS period starts, Z12 turns on siren and logs the onset time

S2,
  A(2)*B(0)#T: Z13; OFF ^greenLight ---> S1 \Z13 turns off siren once cue duration ends
  #R^PortEntry: Z13; OFF^greenLight --->S1 \Z13 turns off siren if a PE is made



S.S.9, \ DS US control (control of US paired with DS)
S1,
  #Z3:--->S2 \wait for Z3 signal that US should be made available

S2,
  A(2)*B(0)#T: --->S1 \if the full cue duration passes, go back to S1 and wait for the next Z3
  #R^PortEntry: ON ^Pump; SET J(B(1))=B(13)/100; ADD B(5)--->S3 \if PE, turn pump on, record onset time, iterate reward count, then move to S3

S3,
  A(4)*B(0)#T: OFF ^Pump; SET B(3)=3; SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49)--->S1 \following pump duration A(4), turn pump off, update B(3) and return to S1 waiting for next Z3 signal



S.S.10, \ NS control \dp added cue lights for duration of cue
S1,
  #Z6: ON ^NSwhiteNoise; SET I(B(18))=B(13)/100; ON ^redLight--->S2

S2,
  A(2)*B(0)#T: OFF ^NSwhiteNoise; OFF^redLight--->S1 \TODO: should a PE here terminate the NS also?



S.S.11, \ Record PE times and durations
S1,
  #K21:--->S2

S2,
  #R^PortEntry: ADD B(6), B(7+B(3)); \same indexing scheme as DS & NS licks here
                SET K(B(6))=B(13)/100, L(B(6))=0, K(B(6)+1)=-987.987, L(B(6)+1)=-987.987--->S3
  #Z9:--->S1

S3, \In this state, PE has occurred and duration is being recorded
  #R^PortEntrySus:--->S3
  #Z9 ! .01": SET L(B(6))=(B(13)-1)/100-K(B(6))--->S2



S.S.12, \ Latency to enter port after DS
S1,
  #Z2: --->S2 \waiting for a Z2 signal saying a DS trial has started

S2, \if the full cue duration (A2) time passes, save placeholder values for this trial, iterate DS trial counter, and go back to state 1 \if a PE is made before this, save PE timestamp, add to # trials with PE, and go to S3
  A(2)*B(0)#T: SET M(B(17))=A(2), O(B(17))=0, B(3)=3; ADD B(17); SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49) --->S1
  #R^PortEntry: ADD B(21); SET O(B(17))=B(13)/100, B(3)=3; SET W(B(49))=B(3); SET W(B(49)+1)=-987.987; ADD B(49)  --->S3

S3, \calculate PE latency for this trial, then iterate DS trial counter \ ?Why 5"? I feel like this should be as short as possible to gurantee the PE ratio calculation in S.S.14 is correct
  5": SET M(B(17))=O(B(17))-H(B(17)); ADD B(17); SET O(B(17)+1)=-987.987--->S1



S.S.13,  \ Latency to enter port after NS
S1,
  #Z6: --->S2

S2,
  A(2)*B(0)#T: SET N(B(18))=A(2); SET P(B(18))=0; ADD B(18)--->S1
  #R^PortEntry: ADD B(22); SET P(B(18))=B(13)/100--->S3

S3,
  5": SET N(B(18))=P(B(18))-I(B(18)); ADD B(18); SET B(3)=5, P(B(18)+1)=-987.987; SET W(B(49))=B(3);  SET W(B(49)+1)=-987.987;ADD B(49)--->S1



S.S.14,  \ Calculates average latency for DS and NS port entries
S1,
   #K21: ARITHMETICMEAN B(25) = M,0,B(17); SHOW 11, DS PE Latency, B(25);
         ARITHMETICMEAN B(26) = N,0,B(18); SHOW 12, NS PE Latency, B(26)--->SX



S.S.15, \ Records time spent in port in each window
S1,
    #K21:--->S2

S2, \ Begin ITI period count of PE time
    #R^PortEntrySus: ADD B(29); ADD B(30+B(3))--->S2



S.S.16,  \ DS Ratio
S1,
   #z2: --->S2 \wait for a Z2 signal saying start DS trial

S2,
  A(2)*B(0)#T: --->S3 \if the cue plays for the whole duration, go to S3
  #R^PortEntry: --->S3 \if a PE is made, go to S3

S3, \calculate DS PE ratio ?not sure why this delay chosen? To be accurate, this would have to be done only after B(21) has a chance to interate in S.S.10
  A(2)*B(0)#T: SET B(23)=B(21)/B(17)--->S1



S.S.17,  \ NS Ratio
S1,
   #z6:--->S2

S2,
  A(2)*B(0)#T: --->S3
  #R^PortEntry: --->S3

S3,
  10": SET B(24)=B(22)/B(18)--->S1



S.S.18, \ Counts the 5 seconds after the first post-DS port entry
s1,
  #Z2: ---> s2

s2,
  #R^PortEntry:--->s3

s3,
  5": z11 ---> s1 \Z11 signals that 5" has passed since first PE after DS



S.S.19, \ Counts the 5 seconds after the first post-NS port entry
s1,
  #Z6: ---> s2

s2,
  #R^PortEntry:--->s3

s3,
  5": z11 ---> s1



S.S.20, \ Record licks & lick timestamps
S1,
  #K21:--->S2 \wait for K21 signal that the ITI has started?

S2,
  #R^Lick: ADD B(36); ADD B(37+B(3)); \TODO: ?there's some kind of ERROR here, potentially with B(3). I have seen NS licks count as DS licks so something is off.
           SET S(B(36))=B(13)/100, S(B(36)+1)=-987.987 ---> S2 \save timestamp of lick into array S, set next lick to highly negative number
           \ITI, PreCue, Cue, and PostCue
  #Z9:--->S1 \if Z9 signals trial end, go back to S1 and wait for next ITI
  #Z2:--->S3 \if Z2 signals DS start, go to S3 and track licks during DS
  #Z6:--->S5 \if Z6 signals NS start, go to S5 and track licks during NS

S3, \Waits for post-DS port entry
  #R^PortEntry: ---> s4 \only count licks if a PE is registered
  A(2)*B(0)#T: ---> s2 \if full cue duration passes, treat as if in ITI?

S4, \Counts licks for 5 sec post DS port entry
  #R^Lick: ADD B(36); ADD B(37+B(3)); ADD B(47); ADD V(B(17)); SET S(B(36))=B(13)/100, S(B(36)+1)=-987.987 ---> s4
  #z11: ---> s2 \if a Z11 signals 5" has passed since first PE during DS, go back to treat as if ITI state

S5, \Waits for post-NS port entry
  #R^PortEntry: ---> s6
  A(2)*B(0)#T: ---> s2

S6, \Counts licks for 5 sec post NS port entry
  #R^Lick: ADD B(36); ADD B(37+B(3)); ADD B(48); ADD U(B(18)); SET S(B(36))=B(13)/100, S(B(36)+1)=-987.987 ---> s4
  #z11: ---> s2



S.S.21, \ DS Lick Ratio
S1,
   #z2: --->S2 \wait for Z2 to signal DS trial start

S2, \TODO: Shouldn't a #R^PortEntry "gate" be used to prevent lick counting here?
  A(2)*B(0)#T: --->S3 \if the full cue duration passes
  #R^Lick: ADD B(43)--->S3 \if there's a lick add to the DS with lick trial count and go to S3

S3,
  A(2)*B(0)#T: SET B(45)=B(43)/B(17)--->S1  \calculate DS lick ratio



S.S.22, \ NS Lick Ratio
S1,
   #z6:--->S2

S2,
  A(2)*B(0)#T: --->S3
  #R^Lick: ADD B(44)--->S3

S3,
  A(2)*B(0)#T: SET B(46)=B(44)/B(18)--->S1



S.S.23, \ Display of trial number, current state, and elapsed secs in current state
S1,
  #Z5: SHOW 1,Delay,B(16)--->S2

S2,
  #K21: SET B(16)=0;
        IF BOX=B(4) [@T1,@F1]
          @T1:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1)+' ('+IntToStr(Round(B[2]))+')',B[12]);~--->S3
          @F1:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1),B[12]);~--->S3
  1":   ADD B(16); \as each second passes, iterate and display second counter B(16)
        SHOW 1,Delay,B(16)--->SX
  #Z9:--->S9

S3,
  #Z4: SET B(16)=0; \ITI display?
       ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' PRE',B[12]);~--->S4
  1":  ADD B(16);
       IF BOX=B(4) [@T2,@F2]
         @T2:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1)+' ('+IntToStr(Round(B[2]))+')',B[12]);~--->SX
         @F2:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1),B[12]);~--->SX
  #Z9:--->S9

S4,
  #K24: SET B(16)=0; \if DS or NS trial start
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' DS',B[12]);~--->S5
  #K26: SET B(16)=0; \if NS period start
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' NS',B[12]);~--->S5
 1":   ADD B(16);
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' PRE',B[12]);~--->SX
  #Z9:--->S9

S5,
  #K25: SET B(16)=0; \if DS or NS period end
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' POSTDS',B[12]);~--->S6
  #K27: SET B(16)=0; \if NS period end
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' POSTNS',B[12]);~--->S6
   1":   ADD B(16); \should this say NS?
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' DS',B[12]);~--->SX
  #Z9:--->S9

S6,
  #K21: SET B(16)=0; \ITI again?
       IF BOX=B(4) [@T3,@F3]
         @T3:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1)+' ('+IntToStr(Round(B[2]))+')',B[12]);~--->S3
         @F3:~ShowCommand(MG,Box,1,'ITI '+IntToStr(Round(B[1])+1),B[12]);~--->S3
  1":   ADD B(16);
        ~ShowCommand(MG,Box,1,'TRL '+IntToStr(Round(B[1]))+' POST',B[12]);~--->SX
  #Z9:--->S9

S9,
  60':--->SX


S.S.24, \ Summary display of PE and Lick data
S1,
  #K21: SHOW 2,Total PE,B(6),
    3, Total Licks,B(36),
    4, DS PE Licks, B(47),
    5, DS PE Ratio, B(23),
    6, NS Licks,B(41),
    7, NS PE Ratio, B(24),
    8, DS PE Latency, B(25),
    9, NS PE Latency, B(26),
    10,Total PT,B(29),
    11,ITI PE, B(7),
    12,ITI PT,B(30),
    13,PreCue PE,B(8),
    14,PreCue PT,B(31),
    15,DS PE,B(9),
    16,DS PT,B(32),
    17,PostDS PE,B(10),
    18,PostDS PT,B(33),
    19,NS PT,B(34),
    20,NS PE,B(11),
    21,PostNS PT,B(35),
    22,PostNS PE,B(12),
    23, TrialState, B(3),
    24, LaserTrial, B(50)--->S2

S2,
  1": SHOW 2,Total PE,B(6),
    3, Total Licks,B(36),
    4, DS PE Licks, B(47),
    5, DS PE Ratio, B(23),
    6, NS Licks,B(41),
    7, NS PE Ratio, B(24),
    8, DS PE Latency, B(25),
    9, NS PE Latency, B(26),
    10,Total PT,B(29),
    11,ITI PE, B(7),
    12,ITI PT,B(30),
    13,PreCue PE,B(8),
    14,PreCue PT,B(31),
    15,DS PE,B(9),
    16,DS PT,B(32),
    17,PostDS PE,B(10),
    18,PostDS PT,B(33),
    19,NS PT,B(34),
    20,NS PE,B(11),
    21,PostNS PT,B(35),
    22,PostNS PE,B(12),
    23,TrialState, B(3),
    24, LaserTrial, B(50)--->SX

  #Z2: SHOW 25, Last Z2 DS start, B(13)/100--->SX

  #Z4: Show 26, Last Z6 NS start, B(13)/100 --->SX


\S.S.26 \Cue Lights

\S1,
 \ #Z12: ON ^greenLight--->S3 \DS Cue
  \#Z6: ON ^redLight--->S2 \NS Cue

\S2,
 \ 3": OFF ^redLight --->S1 \TODO: consider making this entire cue duration?
\S3,
 \ 3": OFF ^greenLight--->S1

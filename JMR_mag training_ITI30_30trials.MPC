\Magazine Training (with TTL pulses for upstairs DS training)

\During magazine training, animals receive 30 non-contingent deliveries of reward, time intervals variable around a mean of 30 sec
\After the reward is delivered, the rat must make a port entry to trigger the start of the next ITI

\Constants
^delay=1
^postdelay=2
^port=7

\INPUTS
^PortEntry = 6
^Lick = 7
\^leftLeverPress = 1
\^rightLeverPress = 3

\OUTPUTS
^rewardTTL=34 \sends signal to TTL, Plexon, etc.
^portInTTL=36    \sends signal to TTL, Plexon, etc.
^portOutTTL=37    \sends signal to TTL, Plexon, etc.
^lickTTL=38 \sends signal to TTL, Plexon, etc.
^houselight=7
^pump=9         \pump 1

\variables
\c=total port entries
\d=trial number
\e=variable ITI
\f=total trials
\t=timer
\l = total lick count

\ITI values. NOTE: Must have an average of 240"
list A=15", 20", 25", 30", 35", 40", 45"

dim b=2000    \total port entry time
dim k=2000	\total lick time

\z1 = End of session

\Houselight
S.S.1,
    s1,
        #start:--->s2
    s2,
        ^delay':on^houselight--->sx


S.S.2,  \Millisecond Timer
    s1,
        #start:--->s2
    s2,
        0.01":set t=t+.01--->s2


S.S.3,  \PE counter
    s1,
        #start:--->s2
    s2,
        #R^port:add c;set b(c)=t--->s2 \This seems to be continuously adding, consider making discrete PE counter

S.S.4   \non-contingent SUCROSE delivery
    s1,
        #start: SET f=30 --->s2
    s2,
        0.1": randd e=A--->s3
    s3,
        e#t:on ^pump, ^rewardTTL; add d--->s4
    s4,
        2":off ^pump, ^rewardTTL--->s5
    s5,
        #R^port:IF d=f [@T1,@F1]
            @T1: z1--->SX
            @F1: --->S2


S.S.5,  \display progress
    s1,
        0.01": show 1, TotPort, c; show 2, Trial,d --->sx


S.S.6,  \session ended at 20 minutes
    s1,
        #start:--->s2
    s2,
        #z1: off^houselight; set b(c+1) = -987.987--->stopabortflush

S.S.7, \Lick counter
  s1,
        #start:--->s2
\When a lick is detected, add 1 to the total lick count L and create a timestamp in the array K equal to current time
    s2,
        #R^lick:add l;set k(l)=t--->s2 

S.S.8, \Port Entry TTL Signal     (Port exit TTL signal is currently absent)
S1,
    #R^PortEntry: ON ^PortInTTL ---> S2
S2,
     #R^PortEntry: ---> S2 \done like this b/c sustained PE sensor is being used (must turn the TTL off after entering or it will stay active)
    .01": OFF ^PortInTTL ---> S1

S.S.9, \Lick TTL Signal
S1,
    #start: ---> S2
S2,
    #R^Lick: ON ^LickTTL ---> S3
S3,
    .01": OFF ^LickTTL--->S2 





